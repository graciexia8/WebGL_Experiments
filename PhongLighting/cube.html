<!doctype html>
<html lang="en-US">
<head>
  
  <meta charset="UTF-8">
  <meta name="viewport" content="width=device-width, initial-scale=1.0">
  <style>
    body {
      background-color: #000000;
    }
    h3 {font-family: Helvetica, sans-serif;
        color: #fff;
        font-size: x-large;
       }
  </style>

</head>

<body onload="InitDemo();">
    <h3 font-fam>Phong Lighting Demo</h3>
    <canvas id = "game-surface" width="800" height="600" >
        browser does not support html5
    </canvas>
    <img id="textureImage" src="image.png" width="0" height="0"></img>
    <script id="vertex-shader" type="not-javascript">
      precision mediump float;

      attribute vec3 vertPosition;
      attribute vec2 vertTexCoord;
      attribute vec3 vertNormal;

      // Data (to be interpolated) that is passed on to the fragment shader
      varying vec3 v_Vertex;
      varying vec3 v_Normal;
      varying vec2 fragTexCoord;
      
      uniform mat4 vm_matrix;
      uniform mat4 matrix;
      
      void main()
      {
        // Perform the model and view transformations on the vertex and pass this
        // location to the fragment shader.
        v_Vertex = vec3( vm_matrix * vec4(vertPosition, 1.0) );
      
        // Perform the model and view transformations on the vertex's normal vector
        // and pass this normal vector to the fragment shader.
        v_Normal = vec3( vm_matrix * vec4(vertNormal, 0.0) );

        fragTexCoord = vertTexCoord;
        gl_Position = matrix * vec4(vertPosition, 1.0);
      }
    </script>

    <script id="fragment-shader" type="not-javascript">
      // Fragment shader program
      precision mediump float;
      
      const vec3 lightDirection = normalize(vec3(0.0, 1.8, 1.8));
      const vec3 lightColor = vec3(1.0, 1.0, 1.0);
      const vec3 ambientColor = vec3(0.5, 0.4, 0.8);
      const float ambientPecentage = 0.25;
    
      const float shininess = 20.0;
      uniform sampler2D u_sampler;

      // Data (to be interpolated) that is passed on to the fragment shader
      varying vec3 v_Vertex;
      varying vec3 v_Normal;
      varying vec2 fragTexCoord;
      
      void main()
      {
        vec3 to_light;
        vec3 vertex_normal;
        vec3 reflection;
        vec3 to_camera;
        float cos_angle;

        vec3 ambient_color;
        vec3 diffuse_color;
        vec3 specular_color;
        vec3 color;

        vec4 texel = texture2D(u_sampler, fragTexCoord);

        ambient_color = (ambientPecentage * ambientColor) * texel.xyz;

        // Calculate a vector from the fragment location to the light source
        to_light = lightDirection - v_Vertex;
        to_light = normalize( to_light );
      
        // The vertex's normal vector is being interpolated across the primitive
        // which can make it un-normalized. So normalize the vertex's normal vector.
        vertex_normal = normalize( v_Normal );

        // Calculate the cosine of the angle between the vertex's normal vector
        // and the vector going to the light.
        cos_angle = dot(vertex_normal, to_light);
        cos_angle = clamp(cos_angle, 0.0, 1.0);

        diffuse_color = texel.xyz * cos_angle;
      
        // Calculate the reflection vector
        reflection = 2.0 * dot(vertex_normal, to_light) * vertex_normal - to_light;
      
        // Calculate a vector from the fragment location to the camera.
        // The camera is at the origin, so negating the vertex location gives the vector
        to_camera = -1.0 * v_Vertex;

        // Calculate the cosine of the angle between the reflection vector
        // and the vector going to the camera.
        reflection = normalize( reflection );
        to_camera = normalize( to_camera );
        cos_angle = dot(reflection, to_camera);
        cos_angle = clamp(cos_angle, 0.0, 1.0);
        cos_angle = pow(cos_angle, shininess);

        // The specular color is from the light source, not the object
        if (cos_angle > 0.0) {
          specular_color = lightColor * cos_angle;
          diffuse_color = diffuse_color * (1.0 - cos_angle);
        } else {
          specular_color = vec3(0.0, 0.0, 0.0);
        }

        color = ambient_color + diffuse_color + specular_color;

        gl_FragColor = vec4(color, texel.a);
      }
    </script>

    <script src="../lib/gl-matrix.js"></script>
    <script src="javascript/model.js"></script>
    <script src="javascript/render.js"></script>
    <script src="javascript/app.js"></script>
</body>